buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.10'
    id 'net.saliman.cobertura' version '2.6.1'
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'net.saliman.cobertura'

group 'org.concordion.plugin.idea'
version '0.12.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

wrapper {
    gradleVersion = '2.12'
}

repositories {
    mavenLocal()
    mavenCentral()
}

intellij {
    pluginName 'IdeaConcordionSupport'
    plugins = ['java', 'junit', 'properties', 'Groovy', 'org.intellij.scala:2019.2.28', 'git4idea']
    version '2019.2.4'
//    sandboxDirectory "$projectDir/idea-sandbox"
    downloadSources false
    updateSinceUntilBuild false
}

ext {
    //193.4386.21
    markdownSupportPlugin = files(
            "${projectDir}/markdownSupport/lib/markdown.jar",
            "${projectDir}/markdownSupport/lib/markdown-0.1.33.jar",
            "${projectDir}/markdownSupport/lib/owasp-java-html-sanitizer.jar",
            "${projectDir}/markdownSupport/lib/resources_en.jar"
    )
    //2.9.7
    markdownNavigatorPlugin = files(
            "${projectDir}/markdownNavigator/lib/autolink-0.6.0.jar",
            "${projectDir}/markdownNavigator/lib/boxed-json.jar",
            "${projectDir}/markdownNavigator/lib/commons-io-2.4.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-parent.jar",
            "${projectDir}/markdownNavigator/lib/fontbox-2.0.8.jar",
            "${projectDir}/markdownNavigator/lib/graphics2d-0.10.jar",
            "${projectDir}/markdownNavigator/lib/idea-multimarkdown.jar",
            "${projectDir}/markdownNavigator/lib/javafx-webview-debugger.jar",
            "${projectDir}/markdownNavigator/lib/Java-WebSocket-1.3.7.jar",
            "${projectDir}/markdownNavigator/lib/javax.json-1.1.4.jar",
            "${projectDir}/markdownNavigator/lib/jgoodies-common-1.4.0.jar",
            "${projectDir}/markdownNavigator/lib/jsoup-1.10.2.jar",
            "${projectDir}/markdownNavigator/lib/openhtmltopdf-core-0.0.1-RC12.jar",
            "${projectDir}/markdownNavigator/lib/openhtmltopdf-jsoup-dom-converter-0.0.1-RC12.jar",
            "${projectDir}/markdownNavigator/lib/openhtmltopdf-pdfbox-0.0.1-RC12.jar",
            "${projectDir}/markdownNavigator/lib/openhtmltopdf-rtl-support-0.0.1-RC12.jar",
            "${projectDir}/markdownNavigator/lib/pdfbox-2.0.8.jar",
            "${projectDir}/markdownNavigator/lib/plantuml-jar-asl-1.2019.4.jar",
            "${projectDir}/markdownNavigator/lib/plugin-util.jar",
            "${projectDir}/markdownNavigator/lib/reverse-regex-util.jar",
            "${projectDir}/markdownNavigator/lib/smart-data.jar",
            "${projectDir}/markdownNavigator/lib/tree-iteration-util.jar",
            "${projectDir}/markdownNavigator/lib/xmpbox-2.0.8.jar"
    )
}

configurations {
    compileWith
}

sourceSets {
    main.compileClasspath += configurations.compileWith

    main.java.srcDirs = ['gen', 'src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['tests']

    testMd.java.srcDirs = ['tests', 'testsMd']
    testMd.compileClasspath += sourceSets.test.compileClasspath
}

dependencies {
    compileWith markdownSupportPlugin
    compileWith markdownNavigatorPlugin

    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

task testMarkdownSupportIntegration(type: Test) {
    classpath += markdownSupportPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDirs = sourceSets.testMd.output.classesDirs
}

task testMarkdownNavigatorIntegration(type: Test) {
    classpath += markdownNavigatorPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDirs = sourceSets.testMd.output.classesDirs
}

check.dependsOn testMarkdownSupportIntegration
check.dependsOn testMarkdownNavigatorIntegration
