buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.4.10'
    id 'net.saliman.cobertura' version '2.6.1'
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'net.saliman.cobertura'

group 'org.concordion.plugin.idea'
version '0.12.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

wrapper {
    gradleVersion = '5.6.3'
}

repositories {
    mavenLocal()
    mavenCentral()
}

intellij {
    pluginName 'IdeaConcordionSupport'
    plugins = ['java', 'junit', 'properties', 'Groovy', 'org.intellij.scala:2019.3.17', 'git4idea']
    version '2019.3'
//    sandboxDirectory "$projectDir/idea-sandbox"
    downloadSources false
    updateSinceUntilBuild false
}

ext {
    //193.5233.63
    markdownSupportPlugin = files(
            "${projectDir}/markdownSupport/lib/markdown.jar",
            "${projectDir}/markdownSupport/lib/markdown-0.1.33.jar",
            "${projectDir}/markdownSupport/lib/owasp-java-html-sanitizer.jar",
            "${projectDir}/markdownSupport/lib/resources_en.jar"
    )
    //2.0.0
    markdownNavigatorPlugin = files(
            "${projectDir}/markdownNavigator/lib/asm-all-asm4.jar",
            "${projectDir}/markdownNavigator/lib/autolink-0.5.0.jar",
            "${projectDir}/markdownNavigator/lib/commons-io-2.4.jar",
            "${projectDir}/markdownNavigator/lib/commons-validator-1.4.1.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-abbreviation.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-anchorlink.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-autolink.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-definition.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-emoji.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-escaped-character.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-footnotes.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-gfm-strikethrough.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-gfm-tables.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-gfm-tasklist.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-jekyll-front-matter.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-spec-example.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-tables.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-toc.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-typographic.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-wikilink.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-ext-yaml-front-matter.jar",
            "${projectDir}/markdownNavigator/lib/flexmark.jar",
            "${projectDir}/markdownNavigator/lib/flexmark-test-util.jar",
            "${projectDir}/markdownNavigator/lib/idea-multimarkdown.jar",
            "${projectDir}/markdownNavigator/lib/idea-multimarkdown-javafx.jar",
            "${projectDir}/markdownNavigator/lib/javax.json-1.0.4.jar",
            "${projectDir}/markdownNavigator/lib/kotlin-reflect.jar",
            "${projectDir}/markdownNavigator/lib/kotlin-runtime.jar",
            "${projectDir}/markdownNavigator/lib/Loboevolution.jar",
            "${projectDir}/markdownNavigator/lib/parboiled-core-asm4.jar",
            "${projectDir}/markdownNavigator/lib/parboiled-java-asm4.jar",
            "${projectDir}/markdownNavigator/lib/pegdown-1.6.7.jar",
            "${projectDir}/markdownNavigator/lib/smart-data.jar"
    )
}

configurations {
    compileWith
}

sourceSets {
    main.compileClasspath += configurations.compileWith

    main.java.srcDirs = ['gen', 'src']
    main.resources.srcDirs = ['resources']
    test.java.srcDirs = ['tests']

    testMd.java.srcDirs = ['tests', 'testsMd']
    testMd.compileClasspath += sourceSets.test.compileClasspath
}

dependencies {
    compileWith markdownSupportPlugin
    compileWith markdownNavigatorPlugin

    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

task testMarkdownSupportIntegration(type: Test) {
    classpath += markdownSupportPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDirs = sourceSets.testMd.output.classesDirs
}

task testMarkdownNavigatorIntegration(type: Test) {
    classpath += markdownNavigatorPlugin
    classpath += sourceSets.testMd.compileClasspath
    classpath += sourceSets.testMd.runtimeClasspath
    testClassesDirs = sourceSets.testMd.output.classesDirs
}

check.dependsOn testMarkdownSupportIntegration
//TODO uncomment once new stable version of markdown navigator is released
//check.dependsOn testMarkdownNavigatorIntegration
